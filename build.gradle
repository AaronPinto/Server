plugins {
    id 'java'
}

version '1.1'

wrapper {
    gradleVersion = '7.4'
    distributionType = Wrapper.DistributionType.ALL
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.google.apis', name: 'google-api-services-gmail', version: 'v1-rev20221219-2.0.0'
    implementation group: 'com.google.apis', name: 'google-api-services-drive', version: 'v3-rev20221219-2.0.0'
    implementation group: 'com.google.api-client', name: 'google-api-client', version: '2.1.1'
    implementation group: 'com.google.api-client', name: 'google-api-client-java6', version: '2.1.1'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client-jetty', version: '1.34.1'
    implementation group: 'com.sun.activation', name: 'jakarta.activation', version: '2.0.1'
    implementation group: 'com.sun.mail', name: 'jakarta.mail', version: '2.0.1'
    implementation group: 'org.json', name: 'json', version: '20230227'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.1'
    implementation group: 'com.azure', name: 'azure-identity', version: '1.7.2'
    implementation group: 'com.microsoft.graph', name: 'microsoft-graph', version: '5.42.0'

    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.0')
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'server.Server'
    }
    zip64 = true
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA"
}
